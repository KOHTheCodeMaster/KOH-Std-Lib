plugins {
    id 'java'
}

// Configure group ID, artifact ID, and version
group 'com.github.kohthecodemaster'
setArchivesBaseName("koh-std-lib")
version '0.0.2'

sourceCompatibility = 11

//                  Publishing To Central Repo
//  <| ================================================================ |>

// Signing
apply plugin: 'signing'
signing {
    sign configurations.archives
}

// Deploying
apply plugin: 'maven'

// Add Javadoc JAR and sources JAR to artifact
task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}
task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}

// Build, sign, and upload
uploadArchives {
    repositories {
        mavenDeployer {

            // Sign POM
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            // Add required metadata to POM
            pom.project {
                name= 'KOH-Std-Lib'
                version '0.0.2'
                packaging= 'jar'
                description= 'This project is a Java-Library with Utilities to ease up the tasks.'
                url= 'https://github.com/kohthecodemaster/KOH-Std-Lib'

                scm {
                    connection= 'scm:git:git://github.com/kohthecodemaster/KOH-Std-Lib.git'
                    developerConnection= 'scm:git:ssh://github.com:kohthecodemaster/KOH-Std-Lib.git'
                    url= 'http://github.com/kohthecodemaster/KOH-Std-Lib'
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id= 'kohthecodemaster'
                        name= 'Harshit Gupta'
                        email= 'kohthecodemaster@gmail.com'
                        organization= 'com.github.kohthecodemaster'
                        organizationUrl= 'http://github.com/kohthecodemaster'
                    }
                }
            }
        }
    }
}

//  <| ================================================================ |>

repositories {
    mavenCentral()
}

dependencies {

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
//    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

}
